apply plugin: 'com.android.application'

buildscript {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        maven { url 'https://maven.aliyun.com/repository/public' }
    }
    dependencies {
        // The Fabric Gradle plugin uses an open ended version to react
        // quickly to Android tooling updates
    }
}

android {
    namespace 'com.ihewro.focus'
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.ihewro.focus"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 31
        versionName "2.17"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    sourceSets { main { res.srcDirs = ['src/main/res', 'src/main/res-night'] } }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'arm64-v8a'
            universalApk true
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    android.applicationVariants.all { variant ->
        def versionName = variant.versionName
        def buildType = variant.buildType.name

        variant.outputs.all { output ->
            def abi = output.getFilter(com.android.build.OutputFile.ABI)
            if (abi == null || abi.isEmpty()) {
                abi = "universal"
            }
            outputFileName = "focus-${versionName}-${buildType}-${abi}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // AndroidX 核心库
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.webkit:webkit:1.7.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'androidx.browser:browser:1.5.0'

    // Glide (替代 ImageLoader)
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.16.0'

    // 网络请求
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    // 数据库
    implementation 'org.litepal.android:kotlin:3.0.0'

    // 数据库调试
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    // 日志
    implementation 'com.blankj:alog:1.8.0'

    // 异常恢复
    implementation 'com.zxy.android:recovery:1.0.0'

    // Toast
    implementation 'com.github.GrenderG:Toasty:1.5.2'

    // 图标
    implementation "com.mikepenz:iconics-core:3.1.0@aar"
    implementation "com.mikepenz:iconics-views:3.1.0@aar"
    implementation 'com.mikepenz:google-material-typeface:3.0.1.2.original@aar'

    // Guava
    api 'com.google.guava:guava:33.0.0-android'

    // HTML 解析
    implementation 'org.jsoup:jsoup:1.17.2'

    // 下拉刷新
    implementation 'io.github.scwang90:refresh-layout-kernel:2.1.0'
    implementation 'io.github.scwang90:refresh-header-material:2.1.0'
    implementation 'io.github.scwang90:refresh-footer-classics:2.1.0'

    // 侧边栏
    implementation('com.mikepenz:materialdrawer:6.1.2@aar') {
        transitive = true
    }

    // 对话框
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'

    // 数据流动
    implementation 'org.greenrobot:eventbus:3.3.1'

    // 侧滑返回
    implementation 'com.github.ChenTianSaber:SlideBack:v0.8.2'

    // 弹窗
    implementation 'com.github.li-xiaojun:XPopup:2.9.0'

    // 多主题
    implementation 'skin.support:skin-support:4.0.5'
    implementation 'skin.support:skin-support-appcompat:4.0.5'
    implementation 'skin.support:skin-support-design:4.0.5'
    implementation 'skin.support:skin-support-cardview:4.0.5'

    // 权限请求
    implementation 'pub.devrel:easypermissions:2.0.1'

// WebView (使用原版本，排除 FileProvider)
    api("com.just.agentweb:agentweb:4.1.4") {
        exclude group: "androidx.core", module: "core"
    }

    // 测试
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // RecyclerView Adapter Helper（保留兼容性）
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.30'

    // PhotoView（图片查看缩放）
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
}
